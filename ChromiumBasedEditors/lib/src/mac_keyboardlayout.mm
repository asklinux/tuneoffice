/*
 * Copyright (C) Ascensio System SIA, 2009-2026
 *
 * This program is a free software product. You can redistribute it and/or
 * modify it under the terms of the GNU Affero General Public License (AGPL)
 * version 3 as published by the Free Software Foundation, together with the
 * additional terms provided in the LICENSE file.
 *
 * This program is distributed WITHOUT ANY WARRANTY; without even the implied
 * warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. For
 * details, see the GNU AGPL at: https://www.gnu.org/licenses/agpl-3.0.html
 *
 * You can contact Ascensio System SIA by email at info@tuneoffice.com
 * or by postal mail at 20A-6 Ernesta Birznieka-Upisha Street, Riga,
 * LV-1050, Latvia, European Union.
 *
 * The interactive user interfaces in modified versions of the Program
 * are required to display Appropriate Legal Notices in accordance with
 * Section 5 of the GNU AGPL version 3.
 *
 * No trademark rights are granted under this License.
 *
 * All non-code elements of the Product, including illustrations,
 * icon sets, and technical writing content, are licensed under the
 * Creative Commons Attribution-ShareAlike 4.0 International License:
 * https://creativecommons.org/licenses/by-sa/4.0/legalcode
 *
 * This license applies only to such non-code elements and does not
 * modify or replace the licensing terms applicable to the Program's
 * source code, which remains licensed under the GNU Affero General
 * Public License v3.
 *
 * SPDX-License-Identifier: AGPL-3.0-only
 */


#import <Cocoa/Cocoa.h>
#import <Carbon/Carbon.h>
#include "mac_keyboardlayout.h"
#include <unicode/locid.h>
#include <string>

#define LANGIDFROMLCID(lcid) ((uint16_t)(lcid))

static std::string NSStringToStdString(NSString* nsString)
{
    if (!nsString) return {};
    const char* utf8Str = [nsString UTF8String];
    if (!utf8Str) return {};
    return std::string(utf8Str);
}

static uint16_t localeNameToLangId(const std::string &localeName)
{
    if (!localeName.empty()) {
        icu::Locale loc(localeName.c_str());
        return LANGIDFROMLCID(loc.getLCID());
    }
    return 0;
}

uint16_t GetKeyboardLayout(void)
{
    TISInputSourceRef src = TISCopyCurrentKeyboardInputSource();
    if (!src) return 0;
    CFArrayRef languages = (CFArrayRef)TISGetInputSourceProperty(src, kTISPropertyInputSourceLanguages);
    CFStringRef lang = nullptr;
    if (languages && CFArrayGetCount(languages) > 0) {
        lang = (CFStringRef)CFArrayGetValueAtIndex(languages, 0);
    }
    if (!lang) {
       CFRelease(src);
       return 0;
    }
    NSLocale *locale = [[NSLocale alloc] initWithLocaleIdentifier:(__bridge NSString*)lang];
    CFRelease(src);

    std::string language = NSStringToStdString([locale objectForKey:NSLocaleLanguageCode]);
    std::string region = NSStringToStdString([locale objectForKey:NSLocaleCountryCode]);
    std::string script = NSStringToStdString([locale objectForKey:NSLocaleScriptCode]);
    
   if (!language.empty()) {
        if (!script.empty())
            language.append("_" + script);
        else
        if (region.empty()) {
            // try prevent neutral language
            UErrorCode status = U_ZERO_ERROR;
            char fullName[ULOC_FULLNAME_CAPACITY];
            uloc_addLikelySubtags(language.c_str(), fullName, ULOC_FULLNAME_CAPACITY, &status);
            if (U_SUCCESS(status)) {
                icu::Locale loc(fullName);
                region = loc.getCountry();
            }
        }

        if (!region.empty())
            language.append("_" + region);

        // fprintf(stderr, "Canonical name: %s\n", language.c_str());
        return localeNameToLangId(language);
    }
    return 0;
}
